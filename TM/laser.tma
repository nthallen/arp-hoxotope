PARTITION
# PARTITION 11: Laser Control
# 991006  Redfine TURN_ON_LIMIT=4 from 2
# Un-Comment out shutdown in laser_failed
# Set UVMon < 0.10 in uv_check
#
# 991204 DISABLE SHUTDOWN CALLOUTS
#
%{
  #define TURN_ON_LIMIT 4
  static double T1_SP, T2_SP;
  static int D1T_OK, D2T_OK;
  #define NIRVANA 5
  static int dye_count = 0;
  #define DYE_COUNT_LIMIT 5
  int turn_on_cycles = 0;
  int laser_on_requested = 0;
  int diodes_on_requested = 0;
  int lhx_overtemp = 0;
%}

{ if ( convert(LHX1T) > 68. ) lhx_overtemp = 1; }

# Validate laser failed instead of laser_eval;
State dye_pump_start {
     +1 > Dye pump on
        {   if (dye_count++ > DYE_COUNT_LIMIT ) {
          msg(2, "dye pump timed out" );    
          Validate laser_failed;    
            }
        }
        depending on (interlock_fail) {
          Validate laser_eval;
        }
        Hold until (DL_Fl) or 5
          else Validate dye_pump_failed;
        Validate laser_eval;
}

State dye_pump_failed {
        { msg(1, "Dye flow not observed"); }
        > Topaz Emission Off
        > Dye pump off
     +5 Validate dye_pump_start;
}

State laser_eval {
        Hold until (TopazSecs > 5) or 10
            else Validate laser_eval_on;
        Validate laser_hx_init;
}

State laser_eval_on {
        { laser_on_requested = 1; }
		# Assume 28V Switched has been on and OK
    +1  > Power 28V Switched On
    +1  > Topaz Frequency 0
    +1  > Topaz KDP SetPoint 3000
        { if ( RLHist0 != 2 && RLHist0 != 4 )
            Validate laser_talking;
        }
    +2  { diodes_on_requested = 1; }
    +1  Validate laser_diodes_turn_on;
}

State laser_hx_init {
    +1  > Power 28V Switched Off
	    "Waiting for LHX1T < 30";
		Hold Until ( convert(LHX1T) < 30 );
	+1	{ lhx_overtemp = 0; }
	+1	> Power 28V Switched On
		Validate laser_hx_init2;
}

State laser_hx_init2 NoLog {
        depending on (1 Hz) {
		  if ( lhx_overtemp )
			Validate laser_hx_init;
		}
	    "Waiting for steady LTE_I current";
	    Hold Until ( convert(LTE_I) < 5 );
		Hold Until ( convert(LTE_I) >= 5 ) or 10
		else Validate laser_off;
		Validate laser_hx_init2;
}

State laser_off {
        { turn_on_cycles = 0; }
        > Laser Power Off
        depending on (1 Hz) {
		  if ( lhx_overtemp )
			Validate laser_hx_init;
          else if ( laser_on_requested )
            Validate laser_on;
        }
}

State laser_on {
        > Laser Power Off
        { if ( turn_on_cycles++ >= TURN_ON_LIMIT ) {
            msg( 2, "Laser never talked to us!" );
            Validate laser_failed;
          }
        }
    +10 > Laser Power On
        Validate laser_init;
}

State laser_failed {
        depending on (1 Hz) {
          if ( lhx_overtemp || ! laser_on_requested )
		     Validate laser_off;
        }
#       Validate shutdown;
}

State laser_init {
        depending on (1 Hz) {
          if ( lhx_overtemp || ! laser_on_requested)
		    Validate laser_off;
        }
        Hold until (TopazSecs == 0) or 20
          else Validate laser_on;
        Hold until (TopazSecs > 5) or 20
          else Validate laser_talking;
        Validate laser_on;
}

State laser_talking {
        depending on (1 Hz) {
          if ( lhx_overtemp || ! laser_on_requested )
		    Validate laser_off;
        }
        { T1_SP = convert(RLSD1T) + 3.0;
          if (T1_SP < 24.0) T1_SP = 26.0;
          else if (T1_SP > 40) T1_SP = 40;
          T2_SP = convert(RLSD2T) + 3.0;
          if (T2_SP < 25.0) T2_SP = 27.0;
          else if (T2_SP > 40) T2_SP = 40;
        }
        { turn_on_cycles = 0;
          D1T_OK = 0;
          D2T_OK = 0;
        }
        { if ( RLHist0 == 1 ) Validate PowerModeA; }
        > Topaz Emission Off
    +1  > Topaz Current 0 Amps
    +1  > Topaz Frequency 0
    +1  > Topaz KDP SetPoint 3000
        # depending on ( ON_GROUND) { Validate laser_high_setpoint; }
    +1  > Topaz Diode 1 Temperature 26.0 C
    +1  > Topaz Diode 2 Temperature 28.0 C
        "Holding for NIRVANA";
        Hold until (RLHist0 != 0) or 5:00
          else {
            msg( 2, "Five minute timeout waiting for NIRVANA" );
            Validate laser_reboot;
          }
        { if (RLHist0 == NIRVANA) Validate laser_ready;
          else {
            msg( 2, "RLHist0 = %u: rebooting", RLHist0 );
            Validate laser_reboot;
          }
        }
}

State laser_high_setpoint {
        depending on (1 Hz) {
          if ( lhx_overtemp || ! laser_on_requested)
		    Validate laser_off;
        }
        { if ( convert(RLSD1T) > T1_SP-1 ) D1T_OK = 1; }
        { if ( convert(RLSD2T) > T2_SP-1 ) D2T_OK = 1; }
        { if ( RLHist0 == 1 ) Validate PowerModeA; }
        "Topaz Diode 1 Temperature xx.x C";
    +1  { if ( !D1T_OK )
            ci_sendfcmd( 0,
              "Topaz Diode 1 Temperature %.1lf C\n",
              T1_SP ); }
        "Topaz Diode 2 Temperature xx.x C";
    +1  { if ( !D2T_OK )
            ci_sendfcmd( 0,
              "Topaz Diode 2 Temperature %.1lf C\n",
              T2_SP); }
    +5  { if ( !D2T_OK )
            ci_sendfcmd( 0,
              "Topaz Diode 2 Temperature %.1lf C\n",
              T2_SP); }
        "Holding to see if temp setpoints got through";
        Hold until (( D1T_OK && D2T_OK ) || RLHist0 != 0 ) or 1:00
          else {
            msg( 1, "One temperature setpoint didn't get through" );
            Validate laser_high_setpoint;
          }
        "Holding for NIRVANA";
        Hold until (RLHist0 != 0) or 6:00
          else {
            msg( 2, "Five minute timeout waiting for NIRVANA" );
            Validate laser_reboot;
          }
        { if (RLHist0 == NIRVANA) Validate laser_reports_ready;
          else {
            msg( 2, "RLHist0 = %u: rebooting", RLHist0 );
            Validate laser_reboot;
          }
        }
}

State laser_reports_ready {
        depending on (1 Hz) {
          if ( lhx_overtemp || ! laser_on_requested)
		    Validate laser_off;
        }
        { if ( RLHist0 == 1 ) Validate PowerModeA;
          else if (RLHist0 != NIRVANA) {
            msg( 2,
              "Topaz status changed from NIRVANA to %u: rebooting",
              RLHist0 );
            Validate laser_reboot;
          }
        }
    +1  > Topaz Diode 1 Temperature 26.0 C
    +1  > Topaz Diode 2 Temperature 28.0 C
        "Holding to make sure real setpoints get through";
        Hold until (convert(RLSD1T) < 26.0+1 &&
		    convert(RLSD1T) > 26.0-1 &&
                    convert(RLSD2T) < 28.0+1 &&
		    convert(RLSD2T) > 28.0-1) or 2:00
          else Validate laser_reports_ready;
        Validate laser_ready;
}

State laser_ready {
    +1  > Topaz Diode 1 Temperature 26.0 C
    +1  > Topaz Diode 2 Temperature 28.0 C
        { if ( RLHist0 == 1 ) Validate PowerModeA;
          else if (RLHist0 != NIRVANA) {
            msg( 2,
              "Topaz status changed from NIRVANA to %u: rebooting",
              RLHist0 );
            Validate laser_reboot;
          }
        }
        depending on (1 Hz) {
          if ( lhx_overtemp || !laser_on_requested )
            Validate laser_off;
          else if ( DL_Fl && diodes_on_requested )
            Validate laser_diodes_turn_on;
        }
}

State PowerModeA {
        { msg( 2, "Power Mode Ready Observed while diodes off" ); }
        Validate laser_on;
}

State laser_diodes_turn_on {
        depending on (1 Hz) {
          if ( lhx_overtemp ||
		       ! (laser_on_requested && diodes_on_requested) )
            Validate laser_diodes_off;
        }
	    { if (!DL_Fl) Validate laser_diodes_off; }
    +1  > Topaz Current 10 Amps
    +1  > Topaz Emission Start
        "Holding to Observe Current Mode";
        Hold Until (RLHist0 == 2 || RLHist0 == 4) or 30
          else Validate laser_reboot;
        "Warmup Time...";
   +60  > Topaz Current 15.0 Amps
   +60  > Topaz Current 18.0 Amps
   +30  Validate laser_diodes_on;
}

State laser_diodes_on {
        "Diodes are on";
        depending on (1 Hz) {
          if ( lhx_overtemp ||
			   ! (laser_on_requested && diodes_on_requested) )
            Validate laser_diodes_off;
        }
        { if (RLHist0 != 2 && RLHist0 != 4) Validate laser_reboot; }
	    { if (!DL_Fl) Validate laser_diodes_off; }
}

State laser_diodes_off {
        > Topaz Emission Off
    +1  > Topaz Current 0 Amps
        Hold until (RLHist0 == NIRVANA) or 10
          else {
            msg( 2,
              "Did not see NIRVANA after Diodes Off: rebooting" );
            Validate laser_reboot;
          }
        Validate laser_ready;
}

State laser_reboot {
        { if ( lhx_overtemp || ! laser_on_requested )
		    Validate laser_off;
		}
        > Topaz Emission Off
    +3  > Topaz Reboot
        Validate laser_init;
}

PARTITION
# PARTITION 12: Dye flow switch
################################################################
# Check dye/tfr flow switch.  Validate shutdown if interlock on.
# Light fail light in shutdown.
#   CONTINUE WORK HERE
################################################################

%{ /* Interlock constants. */
  static int zero_count = 0;
  static int cycle_count = 0;
  /* ZERO_LIMIT is the number of successive online points where
     both GrnP1 and RLSD1 are below their respective
     thresholds before interlock_fail is asserted.
  */
  #define ZERO_LIMIT 20
  #define CYCLE_LIMIT 1
%}
State uv_check {
    "Checking...";
    depending on (laser_diodes_on) {
    if (EtnFlg & 0x02) {
        if (convert(UVMon) <= 0.10){
          if (++zero_count > ZERO_LIMIT) {
            msg( 2, "uv power below limits" );
            zero_count=0;
            if(++cycle_count > CYCLE_LIMIT)
                Validate interlock_fail;
            else Validate interlock_fail;
            }
        } else  
                zero_count=0;
        }
    }
}
State interlock_fail    {
            "FAILED";
            {msg(1, "interlock failure.");  }
#   +1 Validate shutdown;
}
