%{
  #include <stdlib.h> /* for min/max */
  #include "ohf.h"
%}

AD8_5A last_OHCLI;
last_OHCLI = OHCLI;
unsigned long int Ratio;
invalidate Ratio;

state (peakup_ohcell, peakup_axis_2);
validate peakup_ohcell;
state (filter_point);
state (not_peaking, peaking);

depending on (scanning_etn once) {
  if (last_OHCLI >= 5) {
	msg(0, "Peaking up on the OH Cell");
	validate peakup_ohcell;
  } else {
	msg(0, "Peaking up on Axis 2");
	validate peakup_axis_2;
  }
  validate peaking;
}

depending on (OClUV, SOCFG, SOCNG, RepRate2, UVMon, SX2FG) {
  depending on (peakup_ohcell) {
	Ratio = 0x10000;
	Ratio /= (OClUV + 5);
	Ratio *= RepRate2 ? SOCFG : iconvert(SOCNG);
  } else { /* peakup_axis_2 */
	Ratio = 0x100000;
	Ratio /= ((UVMon>>4) + (5<<4));
	Ratio *= SX2FG;
  }
  validate Ratio;
}

depending on (Ratio once, EtnPs) {
  depending on (peaking)
	if (ohf_point(EtnPs, Ratio))
	  validate filter_point;
}

depending on (peaking, not_scanning_etn once) {
  /* We only act if we're peaking up (not at the beginning) */
  while (ohf_end_point()) validate filter_point;
  validate not_peaking;
  
  if ( Peakup_On && ohf_rate() <= 50 && ohf_rate() > 0 ) {
	if (ohf_peak_filter() == 0) {
	  msg(MSG_FAIL, "Found absolutely Nothing");
	  ohf_another_scan(low_limit, high_limit - low_limit, 50);
	} else if (ohf_peak_pos() < ohf_min_pos() + 156) {
	  msg(MSG_WARN, "Peak is Below Scan Region");
	  ohf_another_scan(max(ohf_min_pos(), low_limit + 3000) -3000, 4000, 30);
	} else if (ohf_peak_pos() > ohf_max_pos() - 710) {
	  msg(MSG_WARN, "Peak is Above Scan Region");
	  ohf_another_scan(min(ohf_peak_pos(), high_limit-3000) - 1000, 4000, 30);
	} else if (ohf_rate() > 15) {
	  ohf_another_scan(max(ohf_peak_pos(), 750) - 750, 1800, 10);
	} else ohf_set_position(ohf_peak_pos());
  } else msg(0, "Scan complete: peakup disabled");
}
